/*****************************************************************************/
/* Linker file for LS2085A processors                                        */
/*****************************************************************************/


MEMORY
{
    /* hw_context:             org = 0x00000000,   len = 0x000000C0 --> Reserved for Hardware Context */
    /* presentation:           org = 0x00000140,   len = 0x000002C0 --> Reserved for Frame presentation area  */
    /* shared_ram_heap:        org = _ssram_heap_start,  len = 256*KB - sizeof(shared_ram)--> Reserved for Shared-RAM heap */
    /* dp_ddr:                 org = 0x40000000,   len = 0x40000000 (1GB)--> Reserved for DP-DDR */
    /* sys_ddr:                org = 0x80000000,   len = 0x80000000 (2GB) --> Reserved for System DDR*/
	 /* TLS - Task Local Storage
		TLS size must be set so that :
			- all roc_process_packet TLS placed variables fit into TLS
			- TLS_SECTION_END_ADDR is an address aligned on a 64 bytes boundary
		The data segment is presented in the TLS_SECTION_END_ADDR to _stack_end
		workspace addresses range.
	*/
    tls:                    org = 0x000000C0,   len = 0x00000140
    stack :                 org = 0x00000400,   len = 0x00007C00 /* Start location for Stack, Big stack for init task */
    i_ram:                  org = 0x00fe0000,   len = 0x00020000 /* I-RAM  */
    shared_ram:             org = 0x01000000,   len = 0x00010400 /* Shared-RAM */
    profile_sram:           org = 0x02030000,   len = 0x0000FFFC /* Profile SRAM */
    exception_handlers:     org = 0x40000000,   len = 0x00010000
    pseudo_rom:             org = 0x40010000,   len = 0x00100000
    init:                   org = 0x40110000,   len = 0x00100000
}

SECTIONS
{
    GROUP : {
      .init  : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    GROUP : {
      .exception_data   : {}
      .exception_data_bss   : {}
      .interrupt_vector (VLECODE) ALIGN (2048) : {}
    } > exception_handlers

    GROUP : {
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .dtext (TEXT) ALIGN(0x1000): {}
        .dtext_vle (VLECODE) ALIGN(0x1000): {
         *(.dtext)
         *(.dtext_vle)
         }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {
        .itext (TEXT) ALIGN(0x1000) : {}
        .itext_vle (VLECODE) ALIGN(0x1000): {
             *(.itext)
             *(.itext_vle)
         }
    } > i_ram

    GROUP : {
        .tdata (DATA) : {}
        .tbss (BSS) NO_INIT_BSS : {}
        .verif_tdata   : {}
        .verif_tbss   : {}
   } > tls

    GROUP : {
    __SHARED_RAM_START = . ;
        .aiop_init_data (DATA) : {}
        .aiop_init_data_bss (BSS) NO_INIT_BSS : {}
        .stext (TEXT) ALIGN(0x100): {}
        .stext_vle (VLECODE) ALIGN(0x100): {
         *(.stext)
         *(.stext_vle)
         }

       .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
       .verif_data   : {}
       .verif_bss   : {}
  __SHARED_RAM_END = .;
   } > shared_ram



   GROUP : {
       .psram_data   : {}
       .psram_bss   : {}
    } > profile_sram

}

_stack_addr  = ADDR(stack)+SIZEOF(stack);
_stack_end   = ADDR(stack);
_ssram_addr  = ADDR(__SHARED_RAM_START);
_ssram_end   = ADDR(__SHARED_RAM_END);
_ssram_heap_start = _ssram_end;

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

AIOP_INIT_DATA       = ADDR(shared_ram);
EXCEPTION_HANDLERS   = ADDR(exception_handlers);
TLS_SECTION_END_ADDR = ADDR(tls)+SIZEOF(tls);

/* Definitions for required space of AIOP image in DDR */
/* Used for initialization of aiop_app_init_info ddr size */
AIOP_DDR_START       = ADDR(exception_handlers);
AIOP_DDR_END         = ADDR(init) + SIZEOF(init);
